cmake_minimum_required(VERSION 3.21)

if(${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_BINARY_DIR})
  message(FATAL_ERROR "In-source builds not allowed. Please make a new directory (called a build directory) and run CMake from there. You may need to remove CMakeCache.txt. ")
endif()

project(autd3)

set(VERSION_MAJOR 8 CACHE STRING "Project major version number.")
set(VERSION_MINOR 4 CACHE STRING "Project minor version number.")
set(VERSION_PATCH 1 CACHE STRING "Project patch version number.")
mark_as_advanced(VERSION_MAJOR VERSION_MINOR VERSION_PATCH)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

option(BUILD_ALL "BUILD_ALL" OFF)
option(BUILD_DOC "BUILD_DOC" OFF)

option(AUTD3_USE_EIGEN_EXTERNAL OFF)

option(AUTD3_PARALLEL_FOR "AUTD3_PARALLEL_FOR" OFF)

option(BUILD_GAIN_HOLO "BUILD_GAIN_HOLO" ON)
option(HOLO_PARALLEL_FOR "HOLO_PARALLEL_FOR" ON)
option(BUILD_BACKEND_CUDA "BUILD_BACKEND_CUDA" OFF)
option(BUILD_BACKEND_ARRAYFIRE "BUILD_BACKEND_ARRAYFIRE" OFF)
option(BUILD_BACKEND_BLAS "BUILD_BACKEND_BLAS" OFF)
set(BLAS_LIB_DIR CACHE PATH "BLAS library directory")
set(BLAS_DEPEND_LIB_DIR CACHE PATH "Additional library directory for BLAS")
set(BLAS_INCLUDE_DIR CACHE PATH "BLAS include directory")
option(USE_MKL "USE_MKL" OFF)

option(BUILD_MODULATION_AUDIO_FILE "BUILD_MODULATION_AUDIO_FILE" OFF)

option(BUILD_LINK_SOEM "BUILD_LINK_SOEM" ON)
option(BUILD_LINK_TWINCAT "BUILD_LINK_TWINCAT" OFF)
option(BUILD_LINK_REMOTE_TWINCAT "BUILD_LINK_REMOTE_TWINCAT" OFF)
option(BUILD_LINK_SIMULATOR "BUILD_LINK_SIMULATOR" OFF)
option(BUILD_LINK_REMOTE_SOEM "BUILD_LINK_REMOTE_SOEM" OFF)
option(BUILD_LINK_EMEM "BUILD_LINK_EMEM" OFF)

option(BUILD_GEOMETRY_VIEWER "BUILD_GEOMETRY_VIEWER" OFF)
option(BUILD_SIMULATOR "BUILD_SIMULATOR" OFF)

option(BUILD_EXAMPLES "BUILD_EXAMPLES" ON)
option(DISABLE_EXAMPLES "DISABLE_EXAMPLES" OFF)

option(BUILD_CAPI "BUILD_CAPI" OFF)

option(ENABLE_LINT "ENABLE_LINT" OFF)

option(EXPORT_AUTD_SIMULATOR "EXPORT_AUTD_SIMULATOR" OFF)
option(EXPORT_SOEM_AUTD_SERVER "EXPORT_SOEM_AUTD_SERVER" OFF)

option(BUILD_TEST "BUILD_TEST" OFF)

option(USE_METER "Use meter instead of millimeter" OFF)
option(USE_LEFT_HANDED "Use left-handed coordinate system instead of right-handed" OFF)
option(USE_SINGLE_FLOAT "Use single precision (32 bit) floating number" OFF)

option(ENABLE_BENCH "Enable benchmarks" OFF)

if(DEFINED BUILD_TARGET)
  if(${BUILD_TARGET} STREQUAL ARM32 OR ${BUILD_TARGET} STREQUAL ARM)
    set(AUTD3_BUILD_ARM ON)
  elseif(${BUILD_TARGET} STREQUAL ARM64 OR ${BUILD_TARGET} STREQUAL AARCH64)
    set(AUTD3_BUILD_ARM ON)
  elseif(${BUILD_TARGET} STREQUAL AMD64 OR ${BUILD_TARGET} STREQUAL x86_64 OR ${BUILD_TARGET} STREQUAL x64)
    set(AUTD3_BUILD_ARM OFF)
  endif()
elseif(${CMAKE_SYSTEM_PROCESSOR} STREQUAL ARM32 OR ${CMAKE_SYSTEM_PROCESSOR} STREQUAL ARM)
  set(AUTD3_BUILD_ARM ON)
elseif(${CMAKE_SYSTEM_PROCESSOR} STREQUAL ARM64 OR ${CMAKE_SYSTEM_PROCESSOR} STREQUAL AARCH64)
  set(AUTD3_BUILD_ARM ON)
else()
  set(AUTD3_BUILD_ARM OFF)
endif()

if(BUILD_ALL)
  set(BUILD_GAIN_HOLO ON)
  set(BUILD_MODULATION_AUDIO_FILE ON)
  set(BUILD_LINK_SOEM ON)
  set(BUILD_LINK_REMOTE_SOEM ON)
  set(BUILD_LINK_TWINCAT ON)
  set(BUILD_LINK_REMOTE_TWINCAT ON)
  set(BUILD_LINK_SIMULATOR ON)
  set(BUILD_EXAMPLES ON)
  set(BUILD_CAPI ON)
endif()

if(DISABLE_EXAMPLES)
  set(BUILD_EXAMPLES OFF)
endif()

if(WIN32)
  set(OS win32)
  include(cmakes/win_vs.cmake)
elseif(APPLE)
  set(OS macosx)
  include(cmakes/mac.cmake)
else()
  set(OS linux)
  include(cmakes/linux.cmake)
endif()
include(cmakes/utils.cmake)

if(NOT BUILD_SHARED_LIBS)
  set(CMAKE_POSITION_INDEPENDENT_CODE TRUE)
endif()

if(BUILD_DOC)
  include(${PROJECT_SOURCE_DIR}/cmakes/doxygen.cmake)
endif()

if(ENABLE_LINT)
  include(${PROJECT_SOURCE_DIR}/cmakes/cpplint.cmake)
endif()

if (AUTD3_USE_EIGEN_EXTERNAL)
  find_package(Eigen REQUIRED)
else()
  include(${PROJECT_SOURCE_DIR}/cmakes/eigen.cmake)
endif()

include(${PROJECT_SOURCE_DIR}/cmakes/spdlog.cmake)

add_library(autd3_common INTERFACE)
target_include_directories(autd3_common INTERFACE ${PROJECT_SOURCE_DIR}/include)
target_link_libraries(autd3_common INTERFACE Eigen3::Eigen spdlog::spdlog_header_only)
target_compile_definitions(autd3_common INTERFACE EIGEN_MPL2_ONLY)
if(USE_METER)
  target_compile_definitions(autd3_common INTERFACE AUTD3_USE_METER)
endif()
if(USE_LEFT_HANDED)
  target_compile_definitions(autd3_common INTERFACE AUTD3_USE_LEFT_HANDED)
endif()
if(USE_SINGLE_FLOAT)
  target_compile_definitions(autd3_common INTERFACE AUTD3_USE_SINGLE_FLOAT)
endif()
if(AUTD3_PARALLEL_FOR)
  target_compile_definitions(autd3_common INTERFACE AUTD3_PARALLEL_FOR)
endif()
enable_cxx_compiler_flag_if_supported(autd3_common INTERFACE /bigobj)
enable_cxx_compiler_flag_if_supported(autd3_common INTERFACE /wd4819)
enable_cxx_compiler_flag_if_supported(autd3_common INTERFACE /Zc:__cplusplus)
if(NOT MSVC) # Too noisy
  enable_cxx_compiler_flag_if_supported(autd3_common INTERFACE /Wall)
endif()
enable_cxx_compiler_flag_if_supported(autd3_common INTERFACE /Wextra)
enable_cxx_compiler_flag_if_supported(autd3_common INTERFACE /Werror)
add_library(autd3::common ALIAS autd3_common)

add_subdirectory(bench)
add_subdirectory(src)
add_subdirectory(tests)
add_subdirectory(examples)
add_subdirectory(capi)
add_subdirectory(dist)
